apply plugin: 'com.android.application'

// See stackoverflow.com/questions/28498688
def makeGitDescription = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.acmetensortoys.ctfwstimer"
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 16
        versionName "1.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "gitDescription", "\"${makeGitDescription()}\""
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Cam: testing on hardware, whee
            debuggable true
            buildConfigField "String", "gitDescription", "\"${makeGitDescription()}\""
        }
    }

    flavorDimensions "play"
    productFlavors {
        noplay {
            dimension "play"
        }
        play {
            dimension "play"
        }
    }

    // TODO: Should filter out play+debug since it will overflow dex method
    // limits (yikes)
}

repositories {
    maven {
        url "https://repo.eclipse.org/content/repositories/paho-releases/"
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    api project(":lib")

    api 'androidx.appcompat:appcompat:1.4.1'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.preference:preference:1.2.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // Nah.
    // wearApp project(':wear')
    //// Needed for wear data synchronization; blech
    // playApi 'com.google.android.gms:play-services-wearable:16.0.1'

    api 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    api('org.eclipse.paho:org.eclipse.paho.android.service:1.1.1') {
        exclude module: 'support-v4'
    }

    testImplementation 'junit:junit:4.13.2'
}
